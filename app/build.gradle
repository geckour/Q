apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

kotlin.experimental.coroutines 'enable'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.geckour.q"
        minSdkVersion 24
        versionCode 4
        versionName "1.2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            def releaseSettingGradleFile = new File("${project.rootDir}/app/signing/release.gradle")
            if (releaseSettingGradleFile.exists()) {
                apply from: releaseSettingGradleFile, to: android
            } else {
                throw new GradleException("Missing ${releaseSettingGradleFile.getAbsolutePath()} . Generate the file by copying and modifying ${project.rootDir}/app/signing/release.gradle.sample .")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    androidExtensions {
        experimental = true
    }
    dataBinding.enabled = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://dl.bintray.com/ijabz/maven" }
}

dependencies {
    def firebase_ver = '17.2.1'
    def aac_lifecycle_ver = '2.2.0-rc01'
    def aac_room_ver = '2.2.1'
    def coroutines_ver = '1.2.2'
    def permission_dispatcher_ver = '4.5.0'
    def stetho_ver = '1.5.1'
    def glide_ver = '4.9.0'
    def exo_ver = '2.10.3'
    def exo_ext_ver = '2.10.2'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta3'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.media:media:1.1.0"
    implementation 'com.google.android.material:material:1.2.0-alpha01'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Logging
    implementation "com.google.firebase:firebase-core:$firebase_ver"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Inspect
    implementation "com.facebook.stetho:stetho:$stetho_ver"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_ver"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_ver"

    // JSON
    implementation 'com.google.code.gson:gson:2.8.6'

    // AAC ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$aac_lifecycle_ver"
    implementation "androidx.lifecycle:lifecycle-livedata:$aac_lifecycle_ver"
    implementation "androidx.lifecycle:lifecycle-common-java8:$aac_lifecycle_ver"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // AAC Room
    implementation "androidx.room:room-runtime:$aac_room_ver"
    implementation "androidx.room:room-ktx:$aac_room_ver"
    kapt "androidx.room:room-compiler:$aac_room_ver"
    testImplementation "androidx.room:room-testing:$aac_room_ver"

    // KTX
    implementation 'androidx.fragment:fragment-ktx:1.2.0-rc01'
    implementation 'androidx.activity:activity-ktx:1.1.0-rc01'

    // Permission
    implementation "org.permissionsdispatcher:permissionsdispatcher:$permission_dispatcher_ver"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:$permission_dispatcher_ver"

    // Image processing
    implementation "com.github.bumptech.glide:glide:$glide_ver"
    kapt "com.github.bumptech.glide:compiler:$glide_ver"

    // Player
    implementation "com.google.android.exoplayer:exoplayer-core:$exo_ver"
    implementation("com.github.PaulWoitaschek.ExoPlayer-Extensions:extension-flac:$exo_ext_ver") {
        transitive = false
    }
    implementation("com.github.PaulWoitaschek.ExoPlayer-Extensions:extension-opus:$exo_ext_ver") {
        transitive = false
    }

    // Vertical SeekBar
    implementation 'com.h6ah4i.android.widget.verticalseekbar:verticalseekbar:1.0.0'

    // Audio Tag
    implementation 'net.jthink:jaudiotagger:2.2.5'

    // Hash
    implementation 'commons-codec:commons-codec:1.11'
}

apply plugin: 'com.google.gms.google-services'